alias funcNum R1;
//----------------------get free page--------------------------------------
if(funcNum==1) then
	alias currentPID R2;
	R2=[SYSTEM_STATUS_TABLE+1];

//incrementing WAIT_MEM_COUNT in system status table. it is 4th word of SST
	[SYSTEM_STATUS_TABLE+3]=[SYSTEM_STATUS_TABLE+3]+1;

//MEM_FREE_COUNT will be 0 when memory is full. it is 3rd word of SST
	while([SYSTEM_STATUS_TABLE+2]==0) do
		[PROCESS_TABLE+currentPID*16+4]=WAIT_MEM;
		multipush(R1,R2);
		call MOD_5;
		multipop(R1,R2);
	endwhile;
	[SYSTEM_STATUS_TABLE+3]=[SYSTEM_STATUS_TABLE+3]-1;
	[SYSTEM_STATUS_TABLE+2]=[SYSTEM_STATUS_TABLE+2]-1;
//finding free memory page
	alias iter R2;
	alias FreePage R0;
	iter=83;
	while(iter<128) do
		if([MEMORY_FREE_LIST+iter]==0) then
			[MEMORY_FREE_LIST+iter]=1;
			FreePage=iter;
			return;
		endif;
		iter=iter+1;
	endwhile;
endif;

//----------------------------release page-------------------------------------
if(funcNum==2) then
	alias PageNum R2;
	[MEMORY_FREE_LIST+PageNum]=[MEMORY_FREE_LIST+PageNum]-1;
	if([MEMORY_FREE_LIST+PageNum]==0) then
		[SYSTEM_STATUS_TABLE+2]=[SYSTEM_STATUS_TABLE+2]+1;
		alias PID R3;
		PID=2;
		while(PID<=15) do
			if([PROCESS_TABLE+PID*16+4]==WAIT_MEM) then
				[PROCESS_TABLE+PID*16+4]=READY;
			endif;
		PID=PID+1;
		endwhile;
	endif;
	return;
endif;
